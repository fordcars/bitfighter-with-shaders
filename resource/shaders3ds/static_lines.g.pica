.gsh fixed c4 c0 2

; Uniforms
.fvec lineWidth

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.5)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones
.alias  half  myconst.wwww

; Inputs
.alias inpos1 c0
.alias inclr1 c1
.alias inpos2 c2
.alias inclr2 c3

; Outputs - this time the type *is* used
.out outpos position
.out outclr color

.entry gmain
.proc gmain
	; Calculate half lineWidth
	mov r4.x, lineWidth.x
	mul r4.x, half, r4.x            ; lineWidth/2
	mul r4.y, myconst.z, r4.x       ; -lineWidth/2
	
	; Same for all vertices
	mov outclr, inclr1
	mov r5.z, myconst.z
	mov r5.w, ones
	
	; Right vertex
	setemit 0
	mov r5.xy, inpos1.xy
	add r5.x, r4.x, r5.x    ; x += lineWidth/2
	add r5.y, r4.y, r5.y    ; y -= lineWidth/2
	mov outpos, r5
	emit
	
	; Top vertex
	setemit 1
	mov r5.xy, inpos1.xy
	add r5.y, r4.x, r5.y    ; y += lineWidth/2
	mov outpos, r5
	emit
	
	; Left vertex
	setemit 2, prim
	mov r5.xy, inpos1.xy
	add r5.x, r4.y, r5.x    ; x -= lineWidth/2
	add r5.y, r4.y, r5.y    ; y -= lineWidth/2
	mov outpos, r5
	emit
	
	; Same for all vertices
	mov outclr, inclr2
	mov r5.z, myconst.z
	mov r5.w, ones
	
	; Right vertex
	setemit 0
	mov r5.xy, inpos2.xy
	add r5.x, r4.x, r5.x    ; x += lineWidth/2
	add r5.y, r4.y, r5.y    ; y -= lineWidth/2
	mov outpos, r5
	emit
	
	; Top vertex
	setemit 1
	mov r5.xy, inpos2.xy
	add r5.y, r4.x, r5.y    ; y += lineWidth/2
	mov outpos, r5
	emit
	
	; Left vertex
	setemit 2, prim
	mov r5.xy, inpos2.xy
	add r5.x, r4.y, r5.x    ; x -= lineWidth/2
	add r5.y, r4.y, r5.y    ; y -= lineWidth/2
	mov outpos, r5
	emit
	
	end
.end
